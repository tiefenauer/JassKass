<?xml version="1.0" encoding="utf-8"?>
<base:KassViewBase xmlns:fx="http://ns.adobe.com/mxml/2009" 
				   xmlns:s="library://ns.adobe.com/flex/spark" 
				   xmlns:base="info.tiefenauer.jasskass.kass.views.base.*"
				   implements="info.tiefenauer.jasskass.kass.views.interfaces.IKassView"
				   creationComplete="onCreationComplete(event)"
				   >
	
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.events.FlexEvent;
			
			import spark.events.IndexChangeEvent;
			
			import info.tiefenauer.jasskass.app.util.translate;
			import info.tiefenauer.jasskass.kass.model.interfaces.IBalance;
			import info.tiefenauer.jasskass.kass.model.interfaces.IKassEntry;
			
			[Bindable] private var _entries:ArrayCollection = new ArrayCollection();
			
			[Bindable]
			private function get saldo():String{
				var _saldo:Number = 0;
				for each(var item:IKassEntry in _entries){
					_saldo += item.total;
				}
				return _saldo.toFixed(2);
			}
			private function set saldo(value:String):void{}
			
			//-------------------------------
			// Public functions
			//-------------------------------
			public function refresh():void{
				saldoLabel.text = saldo;
			}
			
			//-------------------------------
			// Getter/Setter
			//-------------------------------
			public function get filterSelection():ButtonBar{
				return sortButtonBar;
			}
			public function get entries():ArrayCollection{
				return _entries;
			}
			public function set entries(value:ArrayCollection):void{
				_entries = value;
				refresh();
			}
			public function get balance():IBalance{
				if (data is IBalance)
					return data as IBalance;
				return null;
			}
			public function set balance(value:IBalance):void{
				data = value;
			}
			
			//-------------------------------
			// Event handlers
			//-------------------------------
			protected function onButtonBarSelectionChanged(event:IndexChangeEvent):void{
				dispatchEvent(new Event(FILTER_SELECTED));
			}
			
			protected function onJassKassListSelectionChanged(event:IndexChangeEvent):void{
				dispatchEvent(new Event(JASS_SELECTED));
			}
			
			protected function onCreationComplete(event:FlexEvent):void{
				sortButtonBar.dataProvider = new ArrayCollection([translate('nach Datum'), translate('nach Betrag')]);
			}
			
		]]>
	</fx:Script>
	
	<base:layout>
		<s:VerticalLayout />
	</base:layout>
	
	<s:HGroup width="100%">
		<s:Label text="Saldo: " width="50%" />
		<s:Label id="saldoLabel" text="{saldo}" width="50%" />
	</s:HGroup>
	
	<s:ButtonBar id="sortButtonBar"
				 change="onButtonBarSelectionChanged(event)"
				 requireSelection="true"
				 width="100%"
				 />
		
	<s:List id="jassKassList"
			dataProvider="{_entries}"
			change="onJassKassListSelectionChanged(event)"
			itemRenderer="info.tiefenauer.jasskass.kass.views.components.IKassEntryRenderer"
			width="100%"
			height="100%"
			/>
		
</base:KassViewBase>
